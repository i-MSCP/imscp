#!/usr/bin/perl

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2014 by internet Multi Server Control Panel
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
#
# @category    i-MSCP
# @copyright   2010-2014 by i-MSCP | http://i-mscp.net
# @author      Daniel Andreca <sci2tech@gmail.com>
# @author      Laurent Declercq <l.declercq@nuxwin.com>
# @link        http://i-mscp.net i-MSCP Home Site
# @license     http://www.gnu.org/licenses/gpl-2.0.html GPL v2

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/..", "$FindBin::Bin/../PerlLib", "$FindBin::Bin/../PerlVendor";

use iMSCP::Debug;
use iMSCP::Bootstrapper;
use iMSCP::Execute;
use iMSCP::Mail;
use Servers::mta;

# Turn off localisation features to force any command output to be in english
$ENV{'LC_MESSAGES'} = 'C';

# Do not clear screen at end of script
$ENV{'IMSCP_CLEAR_SCREEN'} = 0;

newDebug('imscp-dsk-quota.log');

umask(027);

my $bootstrapper = iMSCP::Bootstrapper->getInstance();
exit unless $bootstrapper->lock('/tmp/imscp-dsk-quota.lock', 'nowait');
$bootstrapper->boot({ 'norequirements' => 'yes', 'nolock' => 'yes', 'config_readonly' => 'yes' });

sub quotaFiles
{
	my $rs = 0;

	my $database = iMSCP::Database->factory();

	my $mainDomains = $database->doQuery(
		'domain_id', "SELECT domain_id, domain_name FROM domain WHERE domain_status = 'ok'"
	);

	unless (ref $mainDomains eq 'HASH'){
		error($mainDomains);
		iMSCP::Mail->new()->errmsg($mainDomains);
		$rs = 1;
	} else {
		my $bsdSystem = $^O =~ /bsd$/;
		my $mta = Servers::mta->factory();
		my $virtualMailDir = $mta->{'config'}->{'MTA_VIRTUAL_MAIL_DIR'};

		for (keys %{$mainDomains}) {
			my $domainId = $_;
			my $domainName = $mainDomains->{$domainId}->{'domain_name'};
			my $totalDiskUsage = my $webDiskUsage = my $mailDiskUsage = 0;
			my @cmd = ();

			if ($bsdSystem) { # BSD specific
				push @cmd, {
					'webdir' => [
						$main::imscpConfig{'CMD_DU'},
						'-I backups/*',
						'-I domain_disable_page/*',
						'-I logs/*.log',
						'-I statistics/*',
						'-d 0',
						escapeShell("$main::imscpConfig{'USER_WEB_DIR'}/$domainName")
					]
				};
				
				if(-d "$virtualMailDir/$domainName") {
					push @cmd, {
						'maildir' => [
							$main::imscpConfig{'CMD_DU'}, '-d 0', escapeShell("$virtualMailDir/$domainName")
						]
					};
				}
			} else {
				push @cmd, {
					'webdir' => [
						$main::imscpConfig{'CMD_DU'},
						'--exclude=backups/*',
						'--exclude=domain_disable_page/*',
						'--exclude=logs/*.log',
						'--exclude=statistics/*',
						'-s -B1',
						escapeShell("$main::imscpConfig{'USER_WEB_DIR'}/$domainName")
					]
				};

				if(-d "$virtualMailDir/$domainName") {
					push @cmd, {
						'maildir' => [
							$main::imscpConfig{'CMD_DU'}, '-s', '-B1', escapeShell("$virtualMailDir/$domainName")
						]
					};
				}
			}

			# Getting list of all child domains (sub|als|alssub)
			my $childDomains = $database->doQuery(
				'domain_name',
				"
					SELECT
						alias_name AS domain_name
					FROM
						domain_aliasses
					WHERE
						domain_id = ?
					AND
						alias_status = 'ok'
					UNION
					SELECT
						CONCAT(subdomain_name, '.', domain_name) AS domain_name
					FROM
						subdomain
					INNER JOIN
						domain USING(domain_id)
					WHERE
						domain_id = ?
					AND
						subdomain_status = 'ok'
					UNION
					SELECT
						CONCAT(subdomain_alias_name, '.',  alias_name) AS domain_name
					FROM
						subdomain_alias
					INNER JOIN
						domain_aliasses USING(alias_id)
					WHERE
						domain_id = ?
					AND
						subdomain_alias_status = 'ok'
				",
				$domainId, $domainId, $domainId
			);
			unless(ref $childDomains eq 'HASH') {
				error($childDomains);
				iMSCP::Mail->new()->errmsg($childDomains);
				$rs = 1;
			} elsif(%{$childDomains}) {
				for (keys %{$childDomains}) {
					my $domainMailDir = "$virtualMailDir/$_";

					if (-d $domainMailDir) {
						if ($bsdSystem) { # BSD specific
							push @cmd, {
								'maildir' => [ $main::imscpConfig{'CMD_DU'},  '-d 0 ', escapeShell($domainMailDir) ]
							};
						} else {
							push @cmd, {
								'maildir' => [ $main::imscpConfig{'CMD_DU'}, '-s', '-B1', escapeShell($domainMailDir) ]
							};
						}
					}
				}
			}

			for(@cmd) {
				my ($dirType, $quotaCmd) = %{$_};

				my ($stdout, $stderr);
				$rs = execute("@{$quotaCmd}", \$stdout, \$stderr);
				error($stderr) if $stderr && $rs;
				return $rs if $rs;

				if(defined $stdout) {
					debug($stdout);

					if($dirType eq 'webdir') {
						$stdout =~ /^(\d+)/;
						$webDiskUsage = ($bsdSystem)
							? $1 * 1024 # BSD specific
							: $1 if $1;
					} else {
						$stdout =~ /^(\d+)/;
						$mailDiskUsage += ($bsdSystem)
							? $1 * 1024 # BSD specific
							: $1 if $1;
					}
				}
			}

			$totalDiskUsage = $webDiskUsage + $mailDiskUsage;

			if ($totalDiskUsage > 0) {
				my $sql = '
					UPDATE
						domain
					SET
						domain_disk_usage = ?, domain_disk_file = ?, domain_disk_mail = ?
					WHERE
						domain_id = ?
				';
				my $rdata = $database->doQuery(
					'update', $sql, $totalDiskUsage, $webDiskUsage, $mailDiskUsage, $domainId
				);
				unless (ref $rdata eq 'HASH') {
					error($rdata);
					iMSCP::Mail->new()->errmsg($rdata);
					$rs = 1;
				}

				$rdata = $database->doQuery(
					'update', 'UPDATE quotatallies SET bytes_in_used = ? WHERE name = ?', $totalDiskUsage, $domainName
				);
				unless (ref $rdata eq 'HASH') {
					error($rdata);
					iMSCP::Mail->new()->errmsg($rdata);
					$rs = 1;
				}
			}
		}
	}

	$rs;
}

sub quotaSql
{
	my $rs = 0;
	my $database = iMSCP::Database->factory();

	my $databases = $database->doQuery('Database', 'SHOW DATABASES');
	unless (ref $databases eq 'HASH'){
		error($databases);
		iMSCP::Mail->new()->errmsg($databases);
		return 1;
	}

	my $rdata = $database->doQuery(
		'domain_id', 'SELECT domain_id, domain_name, domain_disk_usage FROM domain WHERE domain_status = ?', 'ok'
	);
	unless (ref $rdata eq 'HASH') {
		error($rdata);
		iMSCP::Mail->new()->errmsg($rdata);
		$rs = 1;
	} elsif(%{$rdata}) {
		for (keys %{$rdata}) {
			my $domainId = $_;
			my $domainName = $rdata->{$domainId}->{'domain_name'};
			my $diskUsage = $rdata->{$domainId}->{'domain_disk_usage'};
			my $size = 0;
			my $sqlSize = 0;

			my $customerDatabases = $database->doQuery(
				'sqld_name', 'SELECT sqld_name FROM sql_database WHERE domain_id = ?', $domainId
			);

			unless (ref $customerDatabases eq 'HASH') {
				error($customerDatabases);
				iMSCP::Mail->new()->errmsg($customerDatabases);
				$rs = 1;
			}

			for (keys %{$customerDatabases}) {
				my $sqlDbName = $_;

				if(grep{$_ eq $sqlDbName} keys %{$databases}) { # Does the database exists?
					my $quotedDbName = $database->quoteIdentifier($sqlDbName);

					my $sqlDbTables = $database->doQuery('1', "SHOW TABLE STATUS FROM $quotedDbName");
					unless (ref $sqlDbTables eq 'HASH') {
						error($sqlDbTables);
						iMSCP::Mail->new()->errmsg($sqlDbTables);
						$rs = 1;
					} elsif(%{$sqlDbTables}) {
						for(keys %{$sqlDbTables}) {
							$size +=
								(($sqlDbTables->{$_}->{'Data_length'}) ? $sqlDbTables->{$_}->{'Data_length'} : 0) +
								(($sqlDbTables->{$_}->{'Index_length'}) ? $sqlDbTables->{$_}->{'Index_length'} : 0);
						}
					}
				} else {
					warning("Database '$sqlDbName' exists only in table 'sql_database'");
					iMSCP::Mail->new()->errmsg("Database '$sqlDbName' exists only in table 'sql_database'");
				}
			}

			if ($size > 0) {
				$sqlSize += $size;
				$size += $diskUsage;

				my $quota = $database->doQuery(
					'update ',
					'UPDATE domain SET domain_disk_usage = ?, domain_disk_sql = ? WHERE domain_id = ?',
					$size,
					$sqlSize,
					$domainId
				);
				unless (ref $quota eq 'HASH') {
					error($quota);
					iMSCP::Mail->new()->errmsg($quota);
					$rs = 1;
				}

				$quota = $database->doQuery(
					'update', 'UPDATE quotatallies SET bytes_in_used = ? WHERE name = ?', $size, $domainName
				);
				unless (ref $quota eq 'HASH') {
					error($quota);
					iMSCP::Mail->new()->errmsg($quota);
					$rs = 1;
				}
			}
		}
	}

	$rs;
}

my $rs = quotaFiles();
$rs |= quotaSql();
$bootstrapper->unlock('/tmp/imscp-dsk-quota.lock');
exit $rs;
