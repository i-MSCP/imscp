#!/usr/bin/perl

# i-MSCP - internet Multi Server Control Panel
# Copyright (C) 2010-2015 by internet Multi Server Control Panel
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
#
# @category     i-MSCP
# @copyright    2010-2015 by i-MSCP | http://i-mscp.net
# @author       Daniel Andreca <sci2tech@gmail.com>
# @author       Laurent Declercq <l.declercq@nuxwin.com>
# @link         http://i-mscp.net i-MSCP Home Site
# @license      http://www.gnu.org/licenses/gpl-2.0.html GPL v2

# TODO: This script should be idempotent.

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/..", "$FindBin::Bin/../PerlLib", "$FindBin::Bin/../PerlVendor";

use iMSCP::Debug;
use iMSCP::Getopt;
use iMSCP::Bootstrapper;
use iMSCP::Dialog;
use iMSCP::Dir;
use iMSCP::File;
use iMSCP::Stepper;
use iMSCP::Database;
use iMSCP::SystemGroup;
use iMSCP::Execute;
use iMSCP::Servers;
use iMSCP::Addons;
use File::Basename;

# Turn off localisation features to force any command output to be in english
$ENV{'LANG'} = 'C';

umask(027);

newDebug('imscp-uninstall.log');

silent(1);

sub startUp
{
	iMSCP::Getopt->debug(1); # Enforce debug mode (logs)
	iMSCP::Bootstrapper->getInstance()->boot();

	0;
}

sub process
{
	_userDialog();

	my @steps = (
		[\&_removeCustomers, 'Removing i-MSCP customers'],
		[\&_uninstallAddons, 'Uninstalling i-MSCP addons'],
		[\&_uninstallServers, 'Uninstalling i-MSCP servers'],
		[\&_uninstallDaemons, 'Uninstalling i-MSCP daemons'],
		[\&_removeCrontabFile, 'Removing i-MSCP crontab file'],
		[\&_removeLogrotateFile, 'Removing i-MSCP logorate file'],
		[\&_removeSysctlFile, 'Removing i-MSCP sysctl file'],
		[\&_removeSqlData, 'Removing i-MSCP SQL data'],
		[\&_removeiMSCPSystemGroup, 'Removing i-MSCP system group'],
		[\&_removeFiles, 'Removing i-MSCP files'],
	);

	my $rs = 0;
	my $step = 1;

	for (@steps){
		$rs = step($_->[0], $_->[1], scalar @steps, $step);
		return $rs if $rs;
		$step++;
	}

	iMSCP::Dialog->getInstance()->endGauge() if iMSCP::Dialog->getInstance()->hasGauge();

	0;
}

sub shutDown
{

	iMSCP::Dialog->getInstance()->msgbox(
"
\\Z1Congratulations\\Zn

iMSCP has been successfully uninstalled.

You can find uninstallation logs at /tmp/imscp-uninstall.log
"
	);

	0;
}

sub _userDialog
{
	iMSCP::Dialog->getInstance()->set('yes-label','Continue');
	iMSCP::Dialog->getInstance()->set('no-label','Exit');
	iMSCP::Dialog->getInstance()->set('defaultno', '');

	if (iMSCP::Dialog->getInstance()->yesno(<<EOF)) {

Welcome to the \\Z4i-MSCP $main::imscpConfig{'Version'}\\Zn uninstaller dialog.

This program will uninstall i-MSCP from your server.

\\Z1WARNING\\Zn

All i-MSCP data will be removed.

\\Z4NOTE\\Zn

During the uninstallation process some or all services might require to be shut down or restarted.

To continue select \\Z4Continue\\Z0
EOF

	iMSCP::Dialog->getInstance()->msgbox(<<EOF);

Uninstallation process has been aborted...
EOF

		exit 0;
	}
}

sub _removeCustomers
{
	my $tables = {
		admin => ['admin_status', "AND `admin_type` = 'user'"],
		domain => 'domain_status',
		domain_aliasses => 'alias_status',
		subdomain => 'subdomain_status',
		subdomain_alias => 'subdomain_alias_status',
		mail_users => 'status',
		htaccess => 'status',
		htaccess_groups => 'status',
		htaccess_users => 'status',
		ssl_certs => 'status'
	};

	my $database = iMSCP::Database->factory();

	my $aditionalCondition;

	while (my ($table, $field) = each %$tables) {
		if(ref $field eq 'ARRAY') {
			$aditionalCondition = $field->[1];
			$field = $field->[0];
		} else {
			$aditionalCondition = ''
		}

		my $rdata = $database->doQuery('dummy', "UPDATE `$table` SET `$field` = 'todelete' $aditionalCondition");
		unless(ref $rdata eq 'HASH') {
			error($rdata);
			return 1;
		}
	}

	iMSCP::Bootstrapper->getInstance()->unlock();

	my ($rs, $stdout, $stderr);
	$rs = execute(
		"$main::imscpConfig{'CMD_PERL'} $main::imscpConfig{'ENGINE_ROOT_DIR'}/imscp-rqst-mngr", \$stdout, \$stderr
	);
	debug($stdout) if $stdout;
	error($stderr) if $stderr && $rs;
	error("Error while deleting customers") if $rs && ! $stderr;
	return $rs if $rs;

	iMSCP::Bootstrapper->getInstance()->lock();

	0;
}

sub _uninstallAddons
{
	my $rs = 0;

	startDetail();

	my @addons = iMSCP::Addons->getInstance()->get();
	my $nbAddons = scalar @addons;
	my $step = 1;

	for(@addons) {
		my $package = "Addons::$_";

		eval "require $package";

		unless($@) {
			my $addon = $package->getInstance();

			if($addon->can('uninstall')) {
				$rs = step(
					sub { $addon->uninstall }, sprintf("Running %s uninstall tasks...", ref $addon),
					$nbAddons,
					$step
            	);

				last if $rs;
			}
		} else {
			error($@);
			$rs = 1;
			last;
		}

		$step++;
	}

	endDetail();

	$rs;
}

sub _uninstallServers
{
	my $rs = 0;

	startDetail();

	my @servers = iMSCP::Servers->getInstance()->get();
	my $nbServers = scalar @servers;
	my $step = 1;

	for(@servers) {
		next if $_ eq 'noserver';

		my $package = "Servers::$_";

		eval "require $package";

		unless($@) {
			my $server = $package->factory();

			if($server->can('uninstall')) {
				$rs = step(
					sub { $server->uninstall },
					sprintf("Running %s uninstall tasks...", ref $server),
					$nbServers,
					$step
				);

				last if $rs;
			}
		} else {
			error($@);
			$rs = 1;
			last;
		}

		$step++;
	}

	endDetail();

	$rs;
}

sub _uninstallDaemons
{
	my ($rs, $stdout, $stderr);

	for my $initScript($main::imscpConfig{'IMSCP_NETWORK_SNAME'}, $main::imscpConfig{'IMSCP_DAEMON_SNAME'}) {
		if(-f "$main::imscpConfig{'INIT_SCRIPTS_DIR'}/$initScript") {
			$rs = execute("$main::imscpConfig{'SERVICE_MNGR'} $initScript stop", \$stdout, \$stderr);
			debug($stdout) if $stdout;
			error($stderr) if $stderr && $rs;
    		return $rs if $rs;

			if($main::imscpConfig{'SERVICE_INSTALLER'} ne 'no') {
				$rs = execute("$main::imscpConfig{'SERVICE_INSTALLER'} -f $initScript remove", \$stdout, \$stderr);
				debug($stdout) if $stdout;
				error($stderr) if $stderr && $rs;
				return $rs if $rs;
			}

			$rs = iMSCP::File->new('filename' => "$main::imscpConfig{'INIT_SCRIPTS_DIR'}/$initScript")->delFile();
			return $rs if $rs;
		}
	}

	if(-x '/bin/systemctl') { # Make systemd aware of the changes above
		my ($stdout, $stderr);
		$rs = execute("/bin/systemctl daemon-reload", \$stdout, \$stderr);
		debug($stdout) if $stdout;
		error($stderr) if $stderr && $rs;
		return $rs if $rs;
	}

	0;
}

sub _removeCrontabFile
{
	my $rs = 0;

	$rs = iMSCP::File->new('filename' =>'/etc/cron.d/imscp')->delFile() if -f '/etc/cron.d/imscp';

	$rs;
}

sub _removeLogrotateFile
{
	my $rs = 0;

	iMSCP::File->new('filename' =>'/etc/logrotate.d/imscp')->delFile() if -f '/etc/logrotate.d/imscp';

	$rs;
}

sub _removeSysctlFile
{
	my $rs = 0;

	iMSCP::File->new('filename' =>'/etc/sysctl.d/imscp.conf')->delFile() if -f '/etc/sysctl.d/imscp.conf';

	$rs;
}

# FIXME: localhost is not always the correct value
sub _removeSqlData
{
	my $database = iMSCP::Database->factory();

	# Remove customer's sql users

	my $rdata = $database->doQuery('sqlu_name', 'SELECT DISTINCT sqlu_name, sqlu_host FROM sql_user');
	unless(ref $rdata eq 'HASH') {
		error($rdata);
		return 1;
	}

	for (keys %{$rdata}) {
		# We do not catch any error and it's expected
		$database->doQuery('delete', 'DROP USER ?@?', $rdata->{$_}->{'sqlu_name'}, $rdata->{$_}->{'sqlu_host'});
	}

	$rdata = $database->doQuery('dummy', 'FLUSH PRIVILEGES');
	unless(ref $rdata eq 'HASH') {
		error($rdata);
		return 1;
	}

	# Remove customer's sql datatabases

	$rdata = $database->doQuery('sqld_name', 'SELECT sqld_name FROM sql_database');
	unless(ref $rdata eq 'HASH') {
		error($rdata);
		return 1;
	}

	$database->doQuery('delete', "DROP DATABASE IF EXISTS `$rdata->{$_}->{'sqld_name'}`") for keys %{$rdata};

	# Remove i-MSCP sql database

	$database->doQuery('delete', "DROP DATABASE IF EXISTS `$main::imscpConfig{'DATABASE_NAME'}`");
	unless(ref $rdata eq 'HASH') {
		error($rdata);
		return 1;
	}

	0;
}

sub _removeiMSCPSystemGroup
{
	iMSCP::SystemGroup->getInstance()->delSystemGroup($main::imscpConfig{'IMSCP_GROUP'});
}

sub _removeFiles
{
	my $rs = 0;

	chdir('/');

	for (
		$main::imscpConfig{'CONF_DIR'}, $main::imscpConfig{'USER_WEB_DIR'}, $main::imscpConfig{'LOG_DIR'},
		$main::imscpConfig{'ROOT_DIR'}, $main::imscpConfig{'CACHE_DATA_DIR'}, $main::imscpConfig{'VARIABLE_DATA_DIR'},
	) {
		$rs = iMSCP::Dir->new('dirname' => $_)->remove();
		return $rs if $rs;
	}

	0;

}

my $rs = 0;

$rs ||= startUp();
$rs ||= process();
$rs ||= shutDown();

exit $rs;
